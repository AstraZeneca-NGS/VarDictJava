apply plugin: 'java'
apply plugin:'application'

version = '1.4.4'


repositories { 
    mavenCentral() 
}

targetCompatibility = "1.7"
sourceCompatibility = "1.7"


afterEvaluate {
    if(project.hasProperty("hprof")) {
        applicationDefaultJvmArgs = ["-Xrunhprof:cpu=times,depth=10", "-Xmx768m"]
    } else {
        applicationDefaultJvmArgs = ["-Xms768m", "-Xmx8g"]
    }
}

dependencies {
    compile 'commons-cli:commons-cli:1.2' 
    compile 'com.edropple.jregex:jregex:1.2_01'
    compile('com.github.samtools:htsjdk:1.140') {
        transitive = false
    }
    testCompile 'junit:junit:4.8.2' 
}

mainClassName = "com.astrazeneca.vardict.Main"

startScripts {
    doLast {
        unixScript.text = unixScript.text.replaceAll(/(DEFAULT_JVM_OPTS=.*)/){ all, group -> 
"""${group}

if [ -n "\$${optsEnvironmentVar}" ] ; then
    DEFAULT_JVM_OPTS=''
fi
"""
        }
    }
}

javadoc {
    options.noQualifiers 'java.lang:java.io:java.util'
    options.memberLevel=JavadocMemberLevel.PACKAGE
    exclude '**/ConcurrentHashSet*'
    exclude '**/Utils*'
    exclude '**/Tuple*'
    exclude '**/Main*'
}


